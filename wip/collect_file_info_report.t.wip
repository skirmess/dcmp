#!perl

use 5.006;
use strict;
use warnings;
use autodie;

use Test::Fatal;
use Test::More 0.88;
use Test::TempDir::Tiny;

use Fcntl ':mode';
use Digest::MD5;

use lib qw(.);

main();

sub main {
    require_ok('bin/dcmp') or BAIL_OUT();

    my $tmpdir = tempdir();
    chdir $tmpdir;

    open my $fh, '>', 'file.txt';
    print $fh "hello world\n";
    close $fh;
    my $file_size = -s 'file.txt';
    my $md5 = Digest::MD5->new();
    $md5->add("hello world\n");
    my $md5_sum = lc $md5->hexdigest();

    symlink 'file.txt', 'valid_link.txt';
    symlink 'invalid_target.txt', 'invalid_link.txt';

    mkdir 'dir';

    like(exception { App::dcmp::_collect_file_info_report('invalid_file') }, qr{ ^ \QCannot stat file invalid_file in $tmpdir: \E }xsm, '_collect_file_info_report throws an exception if lstat failes');

    open $fh, '>', 'invalid_file';
    close $fh;
    chmod 0, 'invalid_file';

    like(exception { App::dcmp::_collect_file_info_report('invalid_file') }, qr{ ^ \QCannot read file invalid_file in $tmpdir: \E }xsm, '_collect_file_info_report throws an exception if it cannot read the file');

    #
    note('dir');
    my $file_info = App::dcmp::_collect_file_info_report('dir');
    is(ref $file_info, ref [], '_collect_file_info_report() returns am array ref');
    is(scalar @{ $file_info}, 2, '... consisting of two values');
    is(${$file_info}[0], 'dir', '... the file name');
    like(${$file_info}[1], qr{ ^ [0-9]+ $ }xsm, '... the mode');
    ok(S_ISDIR(${$file_info}[1]), '... which is from a directory');

    #
    note('file.txt');
    $file_info = App::dcmp::_collect_file_info_report('file.txt');
    is(ref $file_info, ref [], '_collect_file_info_report() returns am array ref');
    is(scalar @{ $file_info}, 4, '... consisting of three values');
    is(${$file_info}[0], 'file.txt', '... the file name');
    like(${$file_info}[1], qr{ ^ [0-9]+ $ }xsm, '... the mode');
    ok(S_ISREG(${$file_info}[1]), '... which is from a file');
    is(${$file_info}[2], $file_size, '... the file size');
    is(${$file_info}[3], $md5_sum, '... the md5 sum');

    #
    note('valid_link.txt');
    $file_info = App::dcmp::_collect_file_info_report('valid_link.txt');
    is(ref $file_info, ref [], '_collect_file_info_report() returns am array ref');
    is(scalar @{ $file_info}, 3, '... consisting of three values');
    is(${$file_info}[0], 'valid_link.txt', '... the file name');
    like(${$file_info}[1], qr{ ^ [0-9]+ $ }xsm, '... the mode');
    ok(S_ISLNK(${$file_info}[1]), '... which is from a symlink');
    is(${$file_info}[2], 'file.txt', '... the links valid target');

    #
    note('invalid_link.txt');
    $file_info = App::dcmp::_collect_file_info_report('invalid_link.txt');
    is(ref $file_info, ref [], '_collect_file_info_report() returns am array ref');
    is(scalar @{ $file_info}, 3, '... consisting of three values');
    is(${$file_info}[0], 'invalid_link.txt', '... the file name');
    like(${$file_info}[1], qr{ ^ [0-9]+ $ }xsm, '... the mode');
    ok(S_ISLNK(${$file_info}[1]), '... which is from a symlink');
    is(${$file_info}[2], 'invalid_target.txt', '... the links invalid target');

    #
    done_testing();

    exit 0;
}

# vim: ts=4 sts=4 sw=4 et: syntax=perl
